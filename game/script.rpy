label start:
    scene bg summer
    "Ну вот, наконец-то последний экзамен за спиной. Лето."
    play music "audio/summer.mp3"
    "Какое прекрасное время года."
    "Деревья переливаются зеленью разнообразных оттенков, приятно обжигают лучи золотистого солнца."
    "Нет больше стресса, нет волнения и будто необъятного и непосильного объема литературы."
    "Только 2 месяца каникул."
    "Рыбалка на речке, прыжки на спор с моста у пруда с пацанами со двора, килограммы мороженного, немного заржавевший, но все также добротно служащий по назначению велосипед."

    "*вторая половина августа*"
    scene bg summer busstop

    "Я шел до автостанции, в последний раз любуясь видами родного города."
    mainHero "*Наверное, мне все-таки повезло купить билет на последний рейс.*"

    "Уже давно вечерело, и бледные холодные столбы жилых домов окрасились оранжевым."
    "Город будто заряжал меня хорошим настроением, указывая на новую путеводную звезду"

    mainHero "*Этот автобус отвезет меня в большой город, в столицу Урала, давая возможности и перспективы, о которых Я даже не думал, пока жил в своей маленькой уютной провинции.*"
    mainHero "*Но пока Я еще не уехал. Я вспоминаю былые истории со своими друзьями, байки местных, праздники и красивые пейзажи, которыми хоть и редко, но баловала меня природа.*"
    mainHero "*Все это Я несу в голове, потому что под такое количество событий не найдется в мире ни сумки, ни вагона.*"
    "Хотя одна сумка меня все же тяготит. Ноутбук явно давал понять, что его мощности не берутся из воздуха. И выбор мной такого зверя был вполне оправдан."
    "Как бы Я ни тратил свое время на отдых, бросить свой проект мне не позволила совесть."
    "Я прочитал несколько книг и статей, потренировался в кодировании, но этого все равно пока недостаточно."
    "Однако Я уверен, что вся эта работа не пропадет даром. То, что мне нужно Я найду в институте."
    "И вот я уже сел в автобус, пристегнулся и уткнулся взглядом в кресло, в ожидании начала поездки"
    "Громко прозвучал прокуренный голос водителя: ВСЕ ПРИСТЕГНУЛИСЬ??. После этого автобус тронулся"

    jump mainBranch

label mainBranch:
    scene black
    "первое сентября.."

    play music "audio/main.mp3"

    scene bg rtf
    unknown "ВНИМАНИЕ, ДОРОГИЕ АБИТУРИЕНТЫ!!! РАЗДАЧА РУЧЕК ПРОХОДИТ НА ПАРКОВКЕ!!!"

    show mainHero_stable at left 

    mainHero "Пусть этот голос и оглушает сильнее торжественной музыки, раздающейся на каждом углу, у меня все равно приподнятое настроение."
    mainHero "Повсюду расписанные баннеры, фотозоны с надписью «Я ЛЮБЛЮ ИРИТ РТФ», группы людей, оживленно обсуждающие сегодняшний день."
    mainHero "На удивление это первое сентября выдалось теплым. Настолько теплым, что Я снова вспомнил, как шел до автобуса. Ну, не будем о грустном в такой праздничный день."
    mainHero "Много новых лиц, со своими чертами характера, со своим стилем. Много новых преподавателей, новые коридоры, новые кабинеты. Первый месяц будет нелегкий..."

    unknown "Ты случайно не с РИ-102664?"

    mainHero "Что?"

    show timur_stable at right
    with dissolve

    unknown "Я не могу найти свою группу, слишком людно. Так какая у тебя группа?"

    mainHero "Ну, моя это группа. Я, кстати, Саша"

    timur "Меня Тимуром звать. Не знаешь, где наши?"

    menu:
        mainHero "Если честно, не имею малейшего понятия. Надо бы их поискать"

        "Давай поищем в здании":
            jump building

        "Мне кажется, они должны быть где то здесь":
            jump hereClose


label building:
    scene bg guk
    show timur_stable:
        xalign 0.0 yalign 1.0
    show mainHero_stable:
        xalign 0.4 yalign 1.0
    with dissolve
    "*Саша и Тимур заходят в институт*"

    timur "Смотри, там стоит какая-то группа, пойдем спросим."

    show kostya_stable at right with dissolve

    unknown "О, еще два мешка подошло."

    "мешка???"

    unknown "Да ладно вам, это шутка такая. Вы с какой группы?"

    timur "РИ-102664"

    unknown "Ха! Как по команде, молодец! За ушком почесать?"

    timur "..."

    mainHero "*Противный тип, надеюсь мы с ним не в одной группе...*"

    show mentor_stable

    unknown "А вот и мои дети!"

    mainHero "*Засада. Я узнал наставницу, он точно из нашей группы*"

    hide mentor_stable

    mainHero "*Я перевел взгляд на Тимура, он был подавлен. Теперь я понял, почему на слове «людно» он занервничал*"
    
    menu:
        mainHero "*Не всем этот день оставит хорошее впечатление. Один среди толпы народа, еще и этот со своими «шутками».*"

        "Ты бы извинился":
            jump imSorry

        "Промолчать":
            jump silence

label imSorry:
    mainHero "Ты бы извинился, твои шутки не несут и капли юмора. Кстати, как тебя там?"

    unknown "Прикуси язык. С такими мягкотелыми общаться — себе во вред. Ты же, наверное, на чистом везении попал сюда, да? Навряд ли ты смог набрать достаточно баллов лишь своим умом."

    mentor "Ну-ка! Что за разговорчики! Саша, Гавриил, прекратите немедленно!!!"

    mainHero "*Кажется, что Тимур воодушевлен тем, что за него кто-то заступился.*"
    $timur_attitude += 1

    "На улице резко затихла музыка. Похоже, пора собираться на торжественное вручение студенческих билетов."

    mentor "Так, нам всем пора на улицу. Бегом!"

    kostya "Мы с тобой еще не закончили..."

    mainHero "Вот и славно. Мне есть чего добавить."
    mainHero "*Мы вышли на улицу. На улице к нам присоединились одногруппники, которые не смогли найти нас сразу.*"

    jump secondChapter

label silence:
    mainHero "*Мне было обидно за Тимура, но что спрашивать с дурака, ему же все как об стену горох.*"

    unknown "Ну, похвалить мне тебя нечем. Могу разве что рассказать немного о ИИ, вы же, наверное, вообще не в теме куда попали."
    unknown "В 1997 году компьютер Deep Blue с системой ИИ обыграл чемпиона мира по шахматам Гарри Каспарова."
    unknown "За 8 лет до этого чемпион заявил, что если будет создан компьютер с рейтингом 2800, то есть равным самого Гарри, Гарри сочтет своим долгом вызвать его на матч, чтобы защитить человеческую расу."
    unknown "Защитничек, похоже, из ваших рядов."

    mainHero "*Да чтоб он язык прикусил, гад.*"

    mentor "Похоже, не все смогли нас найти. Ладно, будем ловить их на улице. Гавриил, ты уже и новеньким лекции читаешь?"

    "На улице резко затихла музыка. Похоже, пора собираться на торжественное вручение студенческих билетов."

    mentor "Так, нам всем пора на улицу. Бегом!"

    "На улице к нам присоединились одногруппники, которые не смогли найти нас сразу."

    jump secondChapter

label hereClose:
    mainHero "*Мы с Тимуром стали искать свою группу среди толпы. Вскоре мы наткнулись на девушку, которая ходила по группам и спрашивала нашу*"

    show cristina_stable

    timur "Привет, ты же ищешь РИ-102664"

    unknown "Привет, да. Вы мои одногруппники? А наставницу не видели?"

    mainHero "Нет, не нашли. Мы пытались найти хоть кого-нибудь."

    unknown "Значит искали медь, а нашли... Медь, получается... Мы же с одной группы..."

    "Упс.. ахах, неловко вышло.."

    cristina "Неудачный каламбур. Ну да ладно, Я Кристина. А вас как зовут?"

    mainHero "Я Саша, а это Тимур."

    cristina "Приятно познакомиться. Музыка здесь довольно громкая, все же."

    timur "Полностью согласен."

    mainHero "*Да, похоже, Тимур не тусовщик, не диджей и точно не ведущий праздников. Шум и толпа народа sявно не для него.*"

    cristina "На самом деле, праздник довольно яркий! Как организаторы постарались для нас, чтобы мы, первокурсники, зарядились энергией на учебу!"

    timur "*ворчливо* Угу, энергия во мне так и бурлит."

    mainHero "Да брось ты. Может, и вправду все будет идти как по маслу."

    timur "По маслу ходить, знаешь ли, не очень-то и здорово."

    menu:
        cristina "*смеется* А вы забавные. Я бы хотела с вами подружиться. Может расскажем друг другу немного о себе? Мне будет так проще вас запомнить"

        "Извини, пока не до знакомств":
            jump omega

        "Конечно, почему бы и нет":
            jump alpha

label omega:
    mainHero "Извини, пока не до знакомств. Вы можете поговорить с Тимуром, а Я еще похожу по стендам с информацией, пока их не оттащили в сторону."
    
    mainHero "*Кристина грустно посмотрела на меня, но, пожав плечами, повернулась к Тимуру и стала выпытывать из него информацию. Ну и настойчивость. Да, пожалуй, не зря Я решил отойти.*"
    $cristina_attitude -= 1

    mainHero "*Я нашел стенды о ИИ. Естественно, там не было никаких схем, кодов, подсказок, однако информация была весьма занимательной.*"

    "«В философии искусственного интеллекта существует гипотеза сильного и слабого ИИ. Ее сторонники считают, что сильный интеллект рано или поздно осознает себя."
    "Понятие слабого интеллекта подобную возможность отрицает.»"
    "«Нет определенного критерия, по которому можно было бы определить, разумна машина или нет. У фантастов на сей счет свое мнение. Многие из них считают, что ИИ становится по-настоящему разумным, когда обретает способность чувствовать.»"
    mainHero "*Я обернулся на вход в институт и увидел Наставницу. Похоже, она вела нашу группу. Я пошел к ним навстречу.*"

    jump secondChapter

label alpha:
    mainHero "Конечно, почему бы и нет."
    mainHero "*Завести новые знакомства не помешало бы. Если так подумать — Я один в этом огромном оживленном городе. Стоило бы найти себе собеседников по интересам.*"
    "Кристина воодушевленно начала рассказывать о себе. Некоторую часть Я не услышал, некоторую не понял, ведь Кристина по мере рассказа говорила все быстрее и быстрее. Суть была в том, что она приехала с какого-то небольшого городка, но хочет добиться большего, чем вернуться обратно и работать на заводе."
    "Она хочет написать ИИ, который бы мог создавать трехмерные модели из двумерных фотографий. Думаю, это может быть полезно для создания чертежей конструкций. Объективно, довольно хорошая перспектива на будущее. Кристина пусть и простодушна на вид, но планирует свою жизнь осознанно."
    $cristina_attitude += 1

    "Похоже, что Кристине понравилось, что кто-то так внимательно выслушал все, что она сказала."

    timur "Ну, мне нечего особо рассказывать. Как и вы, Я с маленького города. Пошел в этот институт по стопам отца. Добровольно."
    timur "Мне нравятся рассказы отца о работе, поэтому хочу пойти по такой же профессии. Мой отец работает радиотехником. Придумывает и собирает устройства."

    mainHero "Пожалуй, нам есть о чем с вами поговорить. Я собираюсь написать ИИ, способного анализировать ситуации, предлагая оптимальные действия для решения вопросов. Проще говоря, кнопка «Подсказка», но в реальной жизни."

    cristina "Да, мы еще вернемся к этой теме..."

    timur "О! Смотрите, там Наставница и, похоже, с нашей группой!"

    "Из здания вышла наша группа. Мы втроем отправились к ним навстречу."

    jump secondChapter

label secondChapter:
    scene bg rtf
    show mainHero_stable:
        xalign -0.1 yalign 1.0
    show cristina_stable:
        xalign 0.1 yalign 1.0
    show timur_stable:
        xalign 0.5 yalign 1.0
    with dissolve

    play audio "audio/ppl.mp3"

    mainHero "*Пока остальные группы подтягивались к крыльцу здания, Я познакомился с одногруппниками.*"
    mainHero "Похоже, рано мы вышли. Видимо, кто-то из толпы зацепил диджейский пульт."
    
    "Наставница решила занять нас какими-то играми на знакомство, на сплочение и прочим. Обычно так делают в детских лагерях, не знаю, почему она решила, что это поможет со студентами."
    "Одногруппники шумели, болтали, не обращали внимание на Наставницу, отчего та в итоге сдалась и занялась своими делами, оставив группу."

    mainHero "Кто-то толкнул меня в спину, отчего Я чуть не упал. Я обернулся. Это был Гавриил."

    hide timur_stable
    hide cristina_stable
    show kostya_stable:
        xalign 0.9 yalign 1.0
    with dissolve

    kostya "Оступился и чуть не задавил букашку, ха!"

    mainHero "*Я уже не могу терпеть его выходки*"
    mainHero "Да что ты вообще возомнил о себе? Ты бы хоть манерам поучился, если уж возомнил себя особой королевской!"

    show timur_stable:
        xalign 0.4 yalign 1.0
    timur "Саша, не надо. Ни к чему сейчас ссориться..."
    kostya "Послушал бы этого недалекого. У вас всех вместе взятых мозгов не хватит, чтобы потягаться со мной. Отец немало денег потратил на мои курсы по программированию и на мой проект!"
    kostya "Мой ИИ может сделать то, что вы бы не сделали за месяц усердной работы! Надеюсь, вам не надо пояснять, что значит «работа»."

    mainHero "Да влей хоть бюджет города в свой пустую голову, она от этого полнее не станет. Думаешь, если у тебя уже есть рабочая версия ИИ, то самый умный и имеешь власть?"
    mainHero "Рискнешь со мной потягаться или за папу спрячешься?"

    timur "Ну зачем ты так? Кристина, помоги уже их разнять..."
    hide timur_stable
    "Пожалуй, про отца было лишнее. Я почувствовал, как за секунду Гавриил вскипел, а его кулак сжался до хруста. Не думал, что смогу когда-нибудь превратить первое сентября в хорошую свадьбу"
    "А какая свадьба хороша без драки?"
    play music "audio/fight.mp3"

    hide kostya_stable
    show kostya_stable:
        xalign 0.9 yalign 1.6
        linear .2 xalign .2 yalign 1.6
    play sound "audio/punch.mp3"
    kostya "*нанес удар*" with Shake((0,0,0,0), 0.5, dist=5)

    mainHero "З-зараза!"
    mainHero "*попал в нос, болит..*"

    play sound "audio/punch.mp3"
    "еще серия из 3 ударов" with Shake((0,0,0,1), 1.5, dist=5) 
    mainHero "*Попал в ухо, скулу, торс.*"

    kostya "Порву гада!"

    play sound "audio/punch.mp3"
    "*удар* *удар*" with Shake((0,0,0,1), 1.5, dist=5)
    mainHero "Повалил тебя, теперь никуда не денешься"


    hide kostya_stable
    hide mainHero_stable
    show timur_stable at right
    show cristina_stable at left
    with dissolve
    timur "Как думаешь, если сейчас встрять, Я сумею выйти целым?"

    cristina "Давай не будем проверять."

    scene bg rtf
    show mentor_stable

    "Вернулась наставница"

    mentor "Вы что творите?! Прекратили живо! Или не получите свои студенческие билеты!!"

    mainHero "*Как невовремя, я почти засадил противному типу в челюсть*"

    mentor "Да как так вообще возможно! Ведете себя как дикие!"
    mentor "*повернулась к группе* А вы почему их не разнимаете? Вам это все развлекуха, Я не пойму?!"

    play music "audio/main.mp3"
    "Музыка стихла снова."
    "На импровизированную сцену вышла Тьютор. Вся толпа студентов вмиг стихла в ожидании речи. Но речь все равно вышла неожиданной."

    scene bg rtf
    show tutor_stable
    with dissolve
    tutor "ДОРОГИЕ АБИТУРИЕНТЫ! ПОЗДРАВЛЯЮ ВАС С ПЕРВЫМ СЕНТЯБРЯ! ДЛЯ ВАС ЭТО..."

    "Да. Каждую фразу она произносила так, будто била в музыкальные тарелки. Ее речь звучала, как сплошная агитация к действиям, как на плакатах времен СССР. Надеюсь, это займет немного времени..."

    "*прошел час*"
    "Кто спал стоя, кто играл в телефоне, кто общался с соседом."
    "Речь тьютора довольно утомила всех. Тимур решил поболтать со мной, чтобы время шло быстрее."


    hide tutor_stable
    show timur_stable at right
    show mainHero_stable at left
    with dissolve

    timur "Слушай, а ты какие компьютерные игры любишь?"

    mainHero "Мне больше поэтапные стратегии нравятся."

    menu:
        timur "Ух ты! Мне тоже! Видел новости? Скоро выходит новая WARSPAMER 52.000. Я бы хотел в нее поиграть. Разработчики говорят, что в этой части появится мультиплеер."

        "Вау! Это получается, что теперь в сюжете будет два военачальника?":
            jump greatness

        "Мне не особо нравится эта серия, слишком сложная для меня. Я скорее казуал.":
            jump casual

label greatness:
    mainHero "Вау! Это получается, что теперь в сюжете будет два военачальника."

    timur "Представляешь? Это же такое поле для тактики и слаженной работы! Какие схемы можно использовать…"

    mainHero "И как Я пропустил такую новость? Теперь тоже буду ждать."

    timur "А может вместе как-нибудь сыграем? Можем поискать что-то похожее, пока игра не вышла."

    mainHero "Конечно, почему бы и нет."

    "Я заметил, как резко воодушевился Тимур, узнав, что Я тоже играю в такие стратегии. Похоже, настоящий трепет внутри у него вызывает выход многообещающей игры, а не слова Тьютора и осознание, что он теперь студент."
    $timur_attitude += 1

    jump thirdChapter

label casual:
    mainHero "Мне не особо нравится эта серия, слишком сложная для меня. Я скорее казуал."

    timur "Оу… Я понял, извини. Не особо разбираюсь в таких, но может еще вернемся к этому."

    "Я не разбираюсь в таких стратегиях, но Тимур, похоже, очень увлечен этой темой. Жаль, что во вкусах мы не сошлись."

    jump thirdChapter

label thirdChapter:
    scene bg rtf
    show tutor_stable

    tutor "А ТЕПЕРЬ ВЫ, УВАЖАЕМЫЕ АБИТУРИЕНТЫ, ОФФИЦИАЛЬНО СТАНЕТЕ СТУДЕНТАМИ! НАСТАВНИКИ, РАЗДАЙТЕ СВОИМ ГРУППАМ СТУДЕНЧЕСКИЕ БИЛЕТЫ!"

    "Наконец-то, долгожданная фраза, долгожданная часть праздника!"
    "Мы все получили студенческие билеты. Все принялись их рассматривать, фотографироваться с ними, но это был еще не конец мероприятия."

    tutor "ВЫ ДОЛЖНЫ ПОНИМАТЬ, ЧТО ПОПАЛИ НЕ В ОБЫЧНЫЙ ВУЗ! ЗДЕСЬ СВОИ РИТУАЛЫ И СВОИ ОСОБЕННОСТИ!"

    mainHero "*Я не успел понять, что она сказала, как мой студенческий взмыл в воздух и повис над моей головой.*"

    "Вокруг послышались удивленные возгласы"

    play sound "audio/fly.mp3"

    "Студенческие начали сиять, затем растворились в воздухе."
    hide tutor_stable
    show mainHero_stable
    mainHero "Что за фокусы?"
    "Внезапно Я почувствовал, будто что-то обвивает указательный палец на левой руке. Я резко поднял руку и увидел…"
    mainHero "Кольцо."

    "В кольце был темно-синий камень, свет в котором преломлялся так, будто за кольцом была безграничная пропасть."
    "Такое кольцо появилось у каждого. Нарастающее жужжание толпы напоминало пчел, улей которых потревожили."
    "Тьютор скомандовала наставникам устроить экскурсию по институту своим группам. Наша группа зашла в здание."

    hide mainHero_stable
    show mentor_stable

    mentor "Так, во-первых, не пугайтесь того, что произошло на улице! Вряд ли найдутся из вас верующие в чудо, но вы слышали Тьютора: это необычный ВУЗ, а значит со своими особенностями."
    mentor "Кольцо будет служить вам хранилищем своих программ, включая ИИ. Расписание, карта института и остальных зданий университета. Как использовать кольцо разберетесь сами, это все равно словами не объяснить."
    mentor "Цвет камня – цвет института, в котором обучается студент. И не пытайтесь сдать кольцо в ломбард, продать как ювелирку и прочее!"
    mentor "Это вам не безделушка. Без него у вас нет доступа в институт. К тому же в чужих руках кольцо превращается обратно в студенческий билет"

    "Я, Тимур и Кристина слушали все это, разинув рты."
    scene bg guk
    show cristina_stable:
        xalign -0.1 yalign 1.0
    show timur_stable:
        xalign 0.5 yalign 1.0
    show mainHero_stable:
        xalign 0.9 yalign 1.0
    "Наставница провела нас по аудиториям, в которых чаще всего у нас будут пары, рассказала о преподавателях, их характерах и кому лучше не просрочивать задания ни на секунду."
    "Будто бы взмывание в воздух бумаги и картона и превращение их в серебро и минерал – такая повседневность."

    stop audio

    "Уже повечерело.. Мы втроем остались осматривать институт, хотя вся группа разбрелась сразу же, как Наставница закончила экскурсию. "
    "Мы рассматривали картины на стенах, заглядывали в аудитории, узнали, где находятся буфет, столовая и прочее"
    "Мы проводили Кристину в ее корпус, затем побрели в свой. "

    "Тимур жил в моем корпусе на два этажа ниже. Попрощавшись с ним на лестничной клетке, уставший Я дошел до своей комнаты и, наконец, рухнул на кровать."
    scene bg living
    show mainHero_stable
    mainHero "*До чего чудное место! А куда ударение ставить, так вообще непонятно. Одно верно – Я попал туда, куда надо. Если здесь творят такое, то уж точно найдутся литературы и материалы для углубленного программирования.*"
    mainHero "Да и ребята в группе, за исключением некоторых индивидов, довольно славные. Особенно Тимур и Кристина. "
    mainHero "Кстати, в комнате же две кровати. Неужели ко мне еще никого не засе…"
    hide mainHero_stable    
    with dissolve   


    show bogdan_stable at right
    show mainHero_stable at left
    with dissolve

    unknown "Да неужели нашел ее! Кто вообще придумал, что в одном общежитии тысяча комнат?!"

    mainHero "Эээ.. Ты о чем?"

    unknown "Как о чем? У нас же 512 комната. А с улицы здание кажется таким маленьким…"

    mainHero "Так ведь… Первое число означает этаж…"
    unknown "???"
    mainHero "..."

    bogdan "Ладно, проехали. Меня, кстати, Богданом зовут. А тебя как?"

    mainHero "Я Саша. Приятно познакомиться. Значит, ты мой сосед?"

    bogdan "Видимо. У меня был вариант остаться жить с родителями, но наш дом на краю города, до института пока дойдешь – пары закончатся. А ты приезжий или местный?"

    mainHero "Приезжий. Из маленького города далеко отсюда."

    bogdan "Это здорово. У любого приезжего историй столько, что с местным и за жизнь не произойдет."

    mainHero "Ну это ты преувеличиваешь. В каком институте учишься?"

    "Мне казалось, что темнота скрывает цвет его кольца, но оказалось не так. Камень в кольце был черный"

    bogdan "В ИНМиТе! А ты?"

    mainHero "В ИРИТ РТФ."

    bogdan "Кру-у-уто. Всякие детальки там, чертежи…"

    mainHero "Да нет, у них новая программа появилась. Обучение специалистов по ИИ программированию."

    bogdan "Значит, пишешь мозги машинам?"

    mainHero "Вроде того. Слушай, поздно уже, а Я с дороги. Давай поболтаем утром, хорошо?"

    bogdan "Да не вопрос. Доброй ночи."

    jump firstCourse