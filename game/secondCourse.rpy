label secondCourse:
    "Кольцо начало сиять синим. Из камня появилась небольшая голограмма. Детище моих плодов наконец подало признаки жизни. Долгожданный первый шаг к цели был сделан."
    
    mainHero "Процесс пошел..."

    scene black
    "второй курс.. на паре.."

    scene bg classroom
    show mainHero_stable at left
    show dima at right
    "К парам по программированию, что не удивительно, Я всегда относился серьезно. Каждое слово, каждое обозначение выводил аккуратнейшим образом. Внимательно слушал и запоминал все, что рассказывал преподаватель."
    "Однако с ИИ это стало чуть проще. Теперь он мог записывать каждую лекцию и выводить ее текстовым документом в небольшое хранилище."
    "Кольцо стало немного подсвечиваться, передо мной появилась размером с фалангу пальца маленькая проекция бесформенного ИИ."

    if firstCharacteristic == "eyes":
        "ИИ показывает на самого себя. Видимо, ему самому нужна эта информация."

        mainHero "Извините, а не могли бы вы привести какой-нибудь пример из сферы… Допустим, из сферы бизнеса."

        dima "Ну а почему бы и нет. Алгоритмическая торговля. Она предполагает использование сложных систем искусственного интеллекта для принятия торговых решений со скоростью, превышающую скорость, на которую способен человеческий организм."
        dima "Это позволяет делать миллионы сделок в день без какого-либо вмешательства человека."

        "Занимательно, но, похоже, ИИ получил что хотел."

    if firstCharacteristic == "ears":
        "ИИ указал на препода, на меня, на студентов…"

        mainHero "*шепотом* Жизнь?"

        "ИИ опять подпрыгнул. Похоже, что ему интересны истории из жизни людей. Надеюсь, это не приведет ни к чему плохому…"

        mainHero "Извините, а какой у вас был жизненный опыт, связанный с искусственным интеллектом? Может, на ваших историях мы можем чему-нибудь поучиться."

        dima "Ну… Есть одна история. Было у нас одно мероприятие давненько, на котором решили опробовать систему распознавания лиц вместо билетов. У входа в здание все еще были турникеты и охрана, но охранникам нужно было лишь, чтобы турникет горел зеленым, остальные вопросы их не касались"
        dima "А как вы знаете, наш институт популярен, да так, что немало у нас иностранных студентов. В общем и целом, система посчитала, что студенты из азиатских стран скрывают свои глаза, а темнокожие студенты скрываются в тени."
        dima "Турникеты загорались красным, охранники выводили «нарушителей» на улицу. Деньги пострадавшим, конечно, вернули, но это еще раз подчеркивает тот факт, что на данный момент искусственный интеллект сырой, ему предстоят еще долгие годы обучения."

        "ИИ записал эту информацию и исчез. Надеюсь, что он меня так никогда не подставит…"

    if firstCharacteristic == "mouth":
        "ИИ начал анализировать аудиторию, преподавателя, студентов. Все, что его окружало. По результатам сбора он выяснил, что рабочий компьютер преподавателя имел вирусы."
        "Но лишь Я собирался об этом сказать, как ИИ вновь обратил все свое внимание на компьютер. Анализ занял даже больше времени, чем в первый раз. Закончив сканирование, ИИ еще долго обрабатывал полученное и, наконец, выдал результат"
        "компьютер был чист"

        mainHero "*Быть не может, чтобы ИИ ошибился… Я ведь очень аккуратно прописывал его анализирующий код, чтобы обучение заняло наименьшее количество времени. Или он все-таки ошибся… Досадно, придется выяснить это в общежитии. *"
    play sound "audio/cringeLaugh.mp3"
    "Я дернулся от того, что парни, сидевшие на ряд выше меня, засмеялись в голос. Один из них со смеху уронил ручку мне за спину. Я вернул им ручку, но посмотрев на преподавателя понял, что сейчас мне влетит вместе с этой парочкой."

    play music "audio/weak.mp3"

    dima "Не изволите ли объясниться, Александр? Вы решили сорвать мне пару?"

    mainHero "Извините, вы не так все поняли…"

    dima "За дурака меня держишь? Я же вижу, как ты донимаешь студентов, сидящих рядом!"

    mainHero "Да нет же…"

    dima "Не желаю слышать и слова! Вы вздумали отвлечь студентов от такой сложной темы! Еще раз замечу за вами подобное и будьте уверены – экзамен вы не сдадите!"

    mainHero "Но..."

    dima "Вон из аудитории!"
    hide mainHero_stable
    show mainHero_stable:
        xalign -0.1 yalign 1.0
        linear .5 xalign 1.7 yalign 1.0

    play music "audio/main.mp3"

    mainHero "*Я вышел аккуратно, закрыв за собой дверь*"
    scene bg koridor1

    show mainHero_stable
    mainHero "*Черт. Это нечестно. Еще и из аудитории выгнали. Придется потратить уйму времени, чтобы разобраться в теме, но ничего уже не поделаешь…*"
    mainHero "*Я пошел до кофемашины, но в коридоре заметил Кристину и Тимура, и решил подойти к ним. Кристина была чем-то сильно взволнована.*"

    show cristina_stable:
        xalign 0.0 yalign 1.0
    show mainHero_stable:
        xalign 0.9 yalign 1.0
    show timur_stable:
        xalign 0.5 yalign 1.0
    with dissolve

    cristina "Саша!"

    mainHero "И вам не хворать, чего суетишься?"

    timur "Привет. Тут такая история: Кристина вышла из аудитории, чтобы ответить на важный звонок, от дверей отошла далеко. Вернувшись, поняла, что пара уже закончилась, соответственно аудиторию закрыли."

    menu:
        mainHero "*Я вопросительно посмотрел на Кристину. Та с грустным выражением лица кивнула головой*"

        "Ну, идем искать у кого можно взять ключи.":
            jump keys

        "Очень жаль, но нам с Тимуром нужно бежать на пару.":
            jump ignore


label keys:
    "Кристина сразу переменилась в лице. Теперь она улыбалась и была заряжена идеей."

    timur "Саша, а как же пара…"

    mainHero "Ничего страшного, это не займет много времени."

    timur "Как скажешь.."

    "Мы отправились к уборщице на нулевом этаже, так как у нее имелись ключи от каждой аудитории."

    "Ключ она нам не дала, однако попросила подождать ее 20 минут. Она закончила свои дела и пошла открывать аудиторию."

    "Тимур очень сильно переживал, что мы опоздали на пару, так как сегодня должна быть контрольная. Мне было немного неловко, что из-за моего решения мы оба попали, но нашу подругу мы бросить не могли."
    "*Кристина забрала свои вещи из аудитории*"

    "Похоже, что Кристина была действительно благодарна"
    $cristina_attitude += 1

    cristina "Ребята, спасибо большое, что остались и помогли мне. Жаль, что вы потратили на это много времени."

    timur "Да ладно уже, не бросать же тебя в беде. А с контрольной что-нибудь придумаем."

    hide cristina_stable
    "Тимур удивил меня своей решимостью в словах, однако только мы разминулись с Кристиной…"
    show timur_stable at left
    show mainHero_stable at right

    timur "Саня, мы в заднице."

    mainHero "Че… Чего?"

    timur "Ты не помнишь? Эта контрольная чуть ли не экзамен по баллам со слов препода. Напишем плохо – работы будет по уши, чуть ниже носа."



    mainHero "*Черт. ЧЕРТ!*"
    mainHero "БЕЖИМ!"
    play sound "audio/firstaudio.mp3"
    scene bg lection
    "От начала пары прошло минут 40. Мы влетели в аудиторию, чуть не выломав двери, Тимур запнулся и врезался в меня, отчего мы оба свалились на пол."
    "Наши синяки оказались напрасны, ведь преподаватель сам опаздывал на пару. Забавная ситуация."

    jump keepGoing

label ignore:
    mainHero "Очень жаль, но нам с Тимуром нужно бежать на пару."

    timur "*грустно вздохнув* Да, у нас сейчас будет контрольная…"

    "Кристина была грустнее самой пасмурной и дождливой осени, но, увы, ничего не поделаешь. Пора идти на пару."
    hide cristina_stable
    $cristina_attitude -= 1

    scene bg lection
    "*спустя 40 минут*"
    "Преподавателя все нет. Отлично. Просто супер."
    "Мало того, что мы не помогли Кристине, так еще и неоправданно. Не думаю, что она это забудет. Хотя, кто же знал, верно?"

    jump keepGoing

label keepGoing:
    "*после пары*"

    scene bg rtf
    show mainHero_stable at left
    show timur_stable at right

    "Мы шли с Тимуром до коворкинга"

    timur "Слушай, Сань… У меня тут проблема возникла…"

    mainHero "В чем дело?"

    timur "Понимаешь… Я вчера сидел за ноутбуком, играл в хоррор один. Я специально выключил свет и раздвинул шторы, чтобы луна освещала комнату… Короче говоря, навел атмосферы. И так увлекся игрой, что…"

    mainHero "..."

    timur "Что во время одного из скримеров подпрыгнул от страха вместе со столом. Ноутбук, естественно, улетел на пол и ударился так, что экран потух."

    menu:
        timur "Как бы Я ни пытался его оживить, ноутбук никак не поддавался. У меня нет денег на мастера, а из друзей у меня только ты. Может быть ты мог посмотреть его? Может, получится что-нибудь сделать?"

        "Герой комнатный. Ладно, посмотрим, не думаю, что что-то критичное.":
            jump repair

        "Извини, Я в компьютерах как в лесу дремучем.":
            jump dontRepair

label repair:
    play music "audio/PC.mp3"

    timur "Отлично! Мне было так грустно, что Я весь день таскаю ноутбук с собой."

    "Мы дошли до коворкинга, сели в кресла. Тимур достал своего страдальца. Отвертки, видимо, тоже от испуга прихватил."

    show notebook

    menu:
        "Мы сняли крышку."

        "Тут повертеть":
            $repairing += 1
            mainHero "Пока не понятно, что мы вообще сделали, но предчуствие хорошее"

        "Там поковырять":
            "Видимо, это не дало никакого результата"

        "Тут потыкать":
            "Видимо, это не дало никакого результата"

    mainHero "Пока непонятно, будем смотреть дальше."
    timur "*встревоженно* Я очень сильно надеюсь, что у нас получится. На нем же все мои работы"

    menu:
        "Что-ж пока что он не заработал, надо что-то делать дальше"

        "Пощелкать клавишу":
            "Похоже, что это не помогло"

        "Открутить детальку":
            "Экран резко загорелся, выдал артефакты в виде радуги, состоящей из букв <S H T C K D>"
            "Саша и Тимур недоуменно переглянулись и продолжили копошиться"
            $repairing += 1

        "Плюнуть туда":
            mainHero "ЧТО Я СДЕЛАЛ? НЕЕЕЕЕЕТ!"
            $repairing -= 1
    mainHero "Ну щас уже точно заработает!!"

    menu:
        "*Черт, не хочется перед Тимуром позориться..."

        "Помолиться":
            timur "Ты дурак? Не смешно вообще-то"

        "Ударить по корпусу":
            "Тимур, мне кажется, умер, воскрес и снова умер после того, как Я это сделал. Но ведь иногда работает же!"
            $repairing += 1

        "Тыкнуть отверткой":
            "Похоже, что это не принесло каких-либо плодов"

    timur "Ну и как там?"
    mainHero "Да я бы тезе сказал как, но тут материться нельзя.."

    menu:
        "Ну же, что там еще делают?"

        "Постучать":
            "Тук-тук. Кто там? Дядя ток?"

        "Продуть":
            "Музыкальные духовые инструменты нашего времени. Через отверстия вентиляции корпуса выходит довольно мелодичный звук"
            $repairing += 1

        "Понажимать клавиши":
            timur "Я не уверен, что дело в этом.."

    if repairing >= 2:
        timur "ПОЛУЧИЛОСЬ! ПОЛУЧИЛОСЬ! САША, ТЫ ЮВЕЛИР!"

        mainHero "Всегда пожалуйста"
        mainHero "*Ноутбук заработал как новый. Надеюсь, что любитель острых ощущений больше не будет выходить за рамки этих самых ощущений и будет беречь этот кладезь сил и трудов своего владельца.*"
    else:
        timur "Не помогло. Зараза… Возможно, родители помогут, хотя будет серьезный разговор."

        mainHero "Извини, старался как мог, но все-таки не спец Я в ноутбуках."

        timur " Да ничего страшного, все равно спасибо огромное что попытался."

    $timur_attitude += 1

    jump secondCourseEnd

label dontRepair:
    "Тимур будто последнюю надежду потерял на восстановление ноутбука."
    timur "*огорченно* Ладно, Я понимаю. Может, родители помогут с деньгами. Правда, добра мне не ждать…"

    mainHero "*Мне было жаль друга, но что если бы Я сделал только хуже? Лучше не рисковать с таким серьезным делом.*"

    jump secondCourseEnd

label secondCourseEnd:
    scene bg library
    show mainHero_stable at left

    play music "audio/meeting.mp3"
    "Я пришел в библиотеку, для меня это давным-давно стало обыденным делом. Чтение книг успокаивало и вносило некоторую ясность в темах, которые для меня были туманными."
    "Но читая очередную книгу Я резко почувствовал головную боль. Ощущения, будто вот-вот моя хрупкая голова лопнет, как арбуз, раздавшись мощным фейерверком из содержимого."
    "В это время мое внимание привлек очень яркий свет от кольца. Кольцо будто собиралось что-то активировать, однако, что можно активировать в месте, окруженным бумагой, деревом и бетоном?"
    "Я перевел взгляд с кольца на книжные полки и тут же заметил…"
    mainHero "Человек"
    mainHero "Зелёный"
    mainHero "Смотрит прямо.. на меня??"

    show ghost at right

    "Не знаю, как Я понял, что он именно смотрит, ведь глаз его Я не увидел. Либо из-за темноты, либо их не было. Да и не думаю, что сам человек был."

    mainHero "*А человек ли? И почему Я вообще продолжаю об этом думать?*"
    mainHero "*Галлюцинации? Но от чего?*"

    "Мне стало очень не по себе. Я покинул библиотеку настолько быстро, насколько мне позволяло мое состояние."
    "В коридоре стало получше. Боль стихла, но и кольцо перестало сиять. Что же это было?"
    "Я решил сходить в уборную, однако лишь Я дошел до ее двери, как боль снова усилилась, а прямо передо мной по коридору прошел такой же зеленый человек. Он больше напоминал голограмму, состоящую из… Строчек кода?"
    mainHero "*А не переутомился ли Я? Что мне уже люди из кодов мерещатся.*"
    mainHero "*Так, если это программы, то может ИИ как-нибудь поможет?*"
    
    menu:
        "Я вызвал ИИ."

        "Скрыться из информационного поля":
            "ИИ включил «глушилку», которая работает по типу плаща-невидимки. Я стал незаметен для сигналов, вся моя техника скрылась. Надеюсь, что с человеком произойдет что-то похожее."
            "И, на удивление, это сработало. Зеленые ушли и больше не появлялись. Мне стало лучше."

        "Попытаться заблокировать зеленых «людей».":
            "ИИ нашел способ заблокировать сигнал зеленых и тут же применил его. Зеленые замерли, как каменные, а затем исчезли."

    hide ghost
    with dissolve

    "Я был неописуемо рад такому исходу событий и помчался в общежитие, так как это было единственное место, в котором Я мог почувствовать себя в безопасности."
    scene bg living
    show mainHero_stable

    play music "audio/main.mp3"
    "Я думаю, вид у меня был такой, будто за мной неслась целая толпа маньяков, терминаторов, ксеноморфов, а возглавлял их Дарт Вейдер, потому что, выбив дверь, Я тут же поймал полный ужаса взгляд Богдана."
    "Я уверен, что если бы он не был в тот момент увлечен видеороликами из интернета, то наверняка бы поседел."
    "Пришлось рассказать все как есть. Богдан, естественно, мне не поверил, но, похоже, мое появление в комнате заставило его выслушать всю историю от начала и до конца."
    "В эту ночь Я еле как смог заснуть."

    jump thirdCourse